plugins {
    id 'java'
    id 'application'
}

group = 'net.vincent'
version = '1.1.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        // This is the version Gradle will look for or download
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Ensures the compiled code is Java 17 compatible
tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

application {
    mainClass = 'net.vincent.communidirect.CommuniDirect'
}

tasks.named('run', JavaExec) {
    standardInput = System.in
}

javadoc {
    destinationDir = file("$rootDir/docs")
}

jar {
    manifest {
        attributes(
            'Main-Class': application.mainClass,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
    from sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
